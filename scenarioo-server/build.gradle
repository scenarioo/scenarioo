/* scenarioo-server
 * Copyright (C) 2014, scenarioo.org Development Team
 * 
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 * 
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 * 
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */


apply plugin: 'war'
apply plugin: 'eclipse-wtp'

sourceCompatibility=1.6
targetCompatibility=1.6

dependencies {
    compile 'org.jboss.resteasy:resteasy-jaxrs:2.3.3.Final'
    compile 'org.jboss.resteasy:resteasy-jaxb-provider:2.3.3.Final'
    compile 'org.jboss.resteasy:resteasy-jackson-provider:2.3.3.Final'
    compile 'org.jboss.resteasy:resteasy-multipart-provider:2.3.3.Final'
    compile 'com.google.guava:guava-base:r03'
    compile 'org.apache.xmlgraphics:batik-transcoder:1.8'
    compile 'org.apache.xmlgraphics:batik-codec:1.8'
    compile 'org.apache.xmlgraphics:xmlgraphics-commons:1.5'
    compile 'com.thetransactioncompany:cors-filter:2.5'
    providedCompile 'javax.servlet:servlet-api:2.5'
}

eclipse {
  wtp {
    component {
      contextPath = 'scenarioo'
      deployName = 'scenarioo'
    }
  }
}

// Prevent Eclipse "Gradle -> Refresh All" to execute cleanEclipseJdt,
// because it would delete our version of the org.eclipse.jdt.ui.prefs file.
cleanEclipse.dependsOn = cleanEclipse.taskDependencies.values - cleanEclipseJdt


task printPathConfigInformationForNodeJSAndGrunt << {
	if (project.hasProperty('pathToNodeJS')) {
        println "Path to Node JS was configued as: " + pathToNodeJS  	
    } else {
    	println "Path to Node JS not specified, set property 'pathToNodeJS' in case the PATH variable is not set properly or not propagated to the client's build script."        
    }
    if (project.hasProperty('installGrunt')) {
        println "installGrunt is specified, this will trigger installation of grunt in client build: " + installGrunt + " (this value should be 'installGrunt')"
    } else {
        println "installGrunt: property is undefined, this means grunt is assumed to be installed already."    
    }
}

task createVersionPropertiesFile << {
	def aggregatorJavaFile = file('src/main/java/org/scenarioo/business/aggregator/ServerVersion.java').text
	def aggregatorVersionRegex = /DERIVED_FILE_FORMAT_VERSION = \"(.*)\"\;/
	def aggregatedDataFormatVersion = ( aggregatorJavaFile =~ aggregatorVersionRegex )[0][1]
	def apiVersion = project.scenariooApiVersion
	
	File versionFile = new File(sourceSets.main.output.classesDir.getAbsolutePath() + '/version.properties');
	versionFile.write('version=' + version + '\nbuild-date=' + new Date() +
		'\napiVersion=' + apiVersion + '\naggregatedDataFormatVersion=' + aggregatedDataFormatVersion)
}

war {
    from '../scenarioo-client/dist'
    from(sourceSets.main.output.classesDir) {
        into('WEB-INF/classes')
    }
	archiveName 'scenarioo-' + version + '.war'
}

war.doLast {
    copy {
        from ('build/libs/')
        into ('build/libs/')
        include('scenarioo-' + version + '.war')
        rename('scenarioo-' + version + '.war', 'scenarioo-latest.war')
    }
}

war.dependsOn createVersionPropertiesFile
war.dependsOn ":scenarioo-client:gulp"